apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.jasonzou.retrofitdemo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // EventBus Index
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.jasonzou.retrofitdemo.eventbus.MyEventBusIndex']
            }
        }
    }

    /*签名配置*/
    signingConfigs {
        appstore {
            keyAlias '中企法顾'
            keyPassword '123456'
            storePassword '123456'
            storeFile file('/Users/jasonzou/Desktop/lcoce.jks')
        }
    }

    //构建模式
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 自定义输出配置
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "RetrofitDemo_${releaseTime()}_V${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                }
            }
            signingConfig signingConfigs.appstore
        }
        debug {
            minifyEnabled true
        }
    }

    flavorDimensions "default"//渠道包相关配置

    /*渠道包*/
    productFlavors {
        xiaomi {
            dimension "default"
            manifestPlaceholders = [TENCENT_ANALYTICS: "小米应用商店",
                                    JPUSH_CHANNEL    : "小米应用商店"]
            signingConfig signingConfigs.appstore
        }
        official {
            dimension "default"
            manifestPlaceholders = [TENCENT_ANALYTICS: "官方",
                                    JPUSH_CHANNEL    : "官方"]
            signingConfig signingConfigs.appstore
        }
    }
    productFlavors.all {
        flavor ->
            flavor.manifestPlaceholders = [TENCENT_ANALYTICS: name,
                                           JPUSH_CHANNEL    : name]
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC+8"))
}

greendao {
    schemaVersion 1//数据库版本号
    daoPackage 'com.jasonzou.retrofitdemo.greendao'//设置DaoMaster、DaoSession、Dao包名
    targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
    // targetGenDirTest //设置生成单元测试目录
//    generateTests true//设置自动生成单元测试用例
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // supportLibraryVersion
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    // 流式布局，FlexboxItemDecoration
    implementation 'com.google.android:flexbox:0.3.2'
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    // rxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    // logger
    implementation 'com.orhanobut:logger:2.2.0'
    // dagger 2
    implementation 'com.google.dagger:dagger:2.17'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'
    /*greenDao*/
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-generator:3.2.2'
    // 图片选择器
    implementation 'com.github.lovetuzitong:MultiImageSelector:1.2'
    /*在线数据库查看器*/
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.0'
    //  事件总线 EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'
    // 二维码第三方依赖库整理
    implementation project(':zxing')
}
